# shellcheck shell=bash
# zshrc extensions
# This file will be loaded in ~/.zprezto/runcoms/zshrc

# Aliases
alias -g 'S!'='>& /dev/null &!'

# Useful function to fix ssh agent forwarding
function fixssh() { eval $(tmux show-env -s |grep '^SSH_') }

# Useful function to clear zprezto auto-complete cache
function compcache_clear() {
    rm -rf "${XDG_CACHE_HOME:-$HOME/.cache}/prezto/zcompdump"
}

# Setup dir_colors
[ -s "$HOME/.dir_colors" ] && eval `dircolors $HOME/.dir_colors`

# Load rtx early, so rtx's tools can be used for auto-completion
export RTX_DATA_DIR="$HOME/.local/apps/rtx"
if command -v rtx > /dev/null ; then
    eval "$(rtx activate zsh)"
fi

# Setup Mambaforge
# Adapted from 'conda init' output to use relative path, so as to improve system-compatiblity
export MAMBA_DIR="$HOME/.local/apps/mambaforge"
if [ -d "$MAMBA_DIR" ]; then
    __conda_setup="$("$MAMBA_DIR/bin/conda" 'shell.zsh' 'hook' 2> /dev/null)"
    if [ $? -eq 0 ]; then
        eval "$__conda_setup"
    else
        if [ -f "$MAMBA_DIR/etc/profile.d/conda.sh" ]; then
            source "$MAMBA_DIR/etc/profile.d/conda.sh"
        else
            export PATH="$MAMBA_DIR/bin:$PATH"
        fi
    fi
    unset __conda_setup

    if [ -f "$MAMBA_DIR/etc/profile.d/mamba.sh" ]; then
        source "$MAMBA_DIR/etc/profile.d/mamba.sh"
    fi
fi

# Setup PHPENV
export PHPENV_ROOT="$HOME/.local/apps/phpenv"
if [ -d "${PHPENV_ROOT}" ]; then
    export PATH="${PHPENV_ROOT}/bin:${PATH}"
    eval "$(phpenv init -)"
fi

# Setup completion for pipx
if PIPX_PATH=$(command -v pipx) ; then
    REAL_PIPX_PATH=$(realpath "$PIPX_PATH")
    PIPX_BIN_DIR=$(dirname "$REAL_PIPX_PATH")
    ARG_COMPLETE_BIN="$PIPX_BIN_DIR/register-python-argcomplete"
    if [[ -f "$ARG_COMPLETE_BIN" ]]; then
        eval "$($ARG_COMPLETE_BIN pipx)"
    fi
fi

# Temporarily add RTX shims to path for tooling setup
old_path=($path)
path=(
    "$RTX_DATA_DIR/shims"
    $path
)

function cmd_path() {
    local COMMAND=$1
    command -v "$COMMAND"
}

function cmd_exists() {
    local COMMAND=$1
    cmd_path $COMMAND 1>/dev/null
}

# Setup completion for terraform
if TERRAFORM_PATH=$(cmd_path terraform) ; then
    autoload -U +X bashcompinit && bashcompinit
    complete -o nospace -C "$TERRAFORM_PATH" terraform
fi

# Setup completion for kubectl
if cmd_exists kubectl ; then
    source <(kubectl completion zsh)

    # Setup kubeswitch
    KUBESWITCH_DIR=$HOME/.local/apps/kubeswitch/current
    [ -s "$KUBESWITCH_DIR/switch.sh" ] && source "$KUBESWITCH_DIR/switch.sh"
fi

# Setup zoxide
if cmd_exists zoxide ; then
    eval "$(zoxide init zsh)"
fi

# Setup direnv
if cmd_exists direnv ; then
    eval "$(direnv hook zsh)"
fi

# Remove RTX shims from path
path=($old_path)

# Load p10k configuration
# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
